= Translation concepts

== Table names to labels

The most simple `SELECT` statement to translate is one without `FROM` clause, such as:

[source,sql,id=t1_1,name=no_driving_table]
----
SELECT 1
----

It is equivalent to a Cypher `RETURN`:

[source,cypher,id=t1_1_expected]
----
RETURN 1
----

`SELECT` statements without `JOIN` clauses are pretty straightforward to translate.
The challenge here is how to map the table name to labels:

* We parse the SQL statement case-sensitive by default
* Table names are mapped to node labels
* Table aliases are used as identifiable symbolic names

[source,sql,id=t1_0,name=select_with_condition]
----
SELECT t.a, t.b
FROM My_Table -- <.>
  AS t -- <.>
WHERE t.a = 1
----
<.> Will be used as the label to match, as-is, i.e. `My_Table`
<.> The table alias will become the node alias

The whole construct will be translated to

[source,cypher,id=t1_0_expected]
----
MATCH (t:My_Table)
WHERE t.a = 1
RETURN t.a, t.b
----

Table aliases are optional, if you omit them, we derive aliases from the labels and types.
If you inspect the translated queries, we recommend using aliases, as this makes the queries better readable.

[#s2c_star_selects]
== Star-Selects

A star- or `*`- select comes in different forms:

Unqualified::
`SELECT * FROM table`
Qualified::
`SELECT t.* FROM table t`

And a variant, selecting the relation itself: `SELECT t FROM table t`.

We make use of this to let you decide whether you want to return Neo4j nodes and relationships as entities, maps, or flattened to individual columns.
The latter requires our translator to have access to the schema of the underlying Neo4j database.
The following sections describe the use-cases.

=== Projecting individual properties

Don't use a star-select but enumerate the properties:

[source,sql,id=star_1]
----
SELECT m.title FROM Movie m
----

The table alias will be used as a symbolic name:

[source,cypher,id=star_1_expected]
----
MATCH (m:Movie)
RETURN m.title;
----

'''

You can omit the table alias:

[source,sql,id=star_2]
----
SELECT title FROM Movie
----

The lower-cased table name will be the symbolic name:

[source,cypher,id=star_2_expected]
----
MATCH (movie:Movie)
RETURN movie.title;
----

'''

Accessing JDBC columns by name leads to code that is hard to maintain, as column renamings impact code as well.
To avoid this, alias the column:

[source,sql,id=star_3]
----
SELECT title AS title FROM Movie
----

So that it has a stable, well-known name:

[source,cypher,id=star_3_expected]
----
MATCH (movie:Movie)
RETURN movie.title AS title;
----

=== Projecting all properties

A `SELECT *` statement gets translated differently depending on whether the connection to the Neo4j database is available.

[source,sql,id=star_5]
----
SELECT * FROM Person p
----

If you are offline, you will get the following Cypher statement:

[source,cypher,id=star_5_expected]
----
MATCH (p:Person) RETURN *
----

The above query will return one column (`p`) containing a Neo4j node.
This is usually not what you expect in the relational world.
If you run the translation online and Neo4j metadata can be retrieved,
you will get a statement that flattens the properties of each node and relationship, plus their element IDs:

In case the `Person` node has properties `born` and `name`,

[source,sql,id=star_6,name=unqualifiedAsteriskSingleTable,metaData=Person:born|name]
----
SELECT * FROM Person p
----

you will get this Cypher statement

[source,cypher,id=star_6_expected]
----
MATCH (p:Person)
RETURN elementId(p) AS `v$id`,
       p.born AS born, p.name AS name
----

This works well with multiple tables as well (`Movie` has properties `title` and `released`):

[source,sql,id=star_7,name=unqualifiedAsteriskMultipleTables,metaData=Person:born|name;Movie:title|released]
----
SELECT * FROM Person p JOIN Movie m ON m.id = p.acted_in
----

[source,cypher,id=star_7_expected]
----
MATCH (p:Person)-[acted_in:ACTED_IN]->(m:Movie)
RETURN elementId(p) AS `v$id`, p.born AS born, p.name AS name,
       elementId(m) AS `v$id1`, m.title AS title, m.released AS released
----

We append increasing numbers to column names to clashing ones (ex. with `name` and `remark` properties both in `Movie` and `Person`):

[source,sql,id=star_8,name=unqualifiedAsteriskDuplicatedColumns,metaData=Person:born|name|remark;Movie:name|released|remark]
----
SELECT * FROM Person p JOIN Movie m ON m.id = p.acted_in
----

Note the increasing numbers per duplicate name:

[source,cypher,id=star_8_expected]
----
MATCH (p:Person)-[acted_in:ACTED_IN]->(m:Movie)
RETURN elementId(p) AS `v$id`,
       p.born AS born, p.name AS name, p.remark AS remark,
       elementId(m) AS `v$id1`,
       m.name AS name1, m.released AS released, m.remark AS remark1
----

'''

The following example uses a join-table to access relationships (we explain this later in this manual <<joinin-relationships, when discussing joins>>), and the flattening of properties works here as well:

[source,sql,id=star_9,name=unqualifiedAsteriskJoinTable, metaData=ACTED_IN:role;Person:born|name;Movie:title|released,table_mappings=people:Person;movies:Movie;movie_actors:ACTED_IN]
----
SELECT *
FROM people p
JOIN movie_actors r ON r.person_id = p.id
JOIN movies m ON m.id = r.person_id
----

[source,cypher,id=star_9_expected]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
RETURN elementId(p) AS `v$id`,
       p.born AS born, p.name AS name,
       elementId(p) AS `v$person_id`,
       elementId(r) AS `v$id1`, r.role AS role,
       elementId(m) AS `v$movie_id`,
       elementId(m) AS `v$id2`,
       m.title AS title, m.released AS released
----

'''

[source,sql,id=star_6b,name=unqualifiedAsteriskSingleTable,metaData=Person:born|name]
.Ordering without specifying a table alias
----
SELECT * FROM Person p ORDER BY name ASC
----

[source,cypher,id=star_6b_expected]
----
MATCH (p:Person)
RETURN elementId(p) AS `v$id`,
       p.born AS born, p.name AS name
ORDER BY p.name
----

'''

A qualified alias can be used as well.
If no Neo4j metadata is available, you will get a map of properties of the node/relationship:

[source,sql,id=star_4,name=mapQualifiedAsteriskWithoutMetadata]
----
SELECT m.*, p.*
FROM Person p
JOIN Movie m ON m.id = p.acted_in
----

The corresponding columns must be downcast to a map in JDBC:

[source,cypher,id=star_4_expected]
----
MATCH (p:Person)-[acted_in:ACTED_IN]->(m:Movie)
RETURN m{.*} AS m, p{.*} AS p
----

'''

If we add more data (ex. `born` and `name` to `Person`), the qualified star will project all of them (note how we also project one single, known column from the `Movie` table):

[source,sql,id=star_4a,name=mapQualifiedAsteriskWithMetadata,metaData=Person:born|name]
----
SELECT p.*, m.title AS title
FROM Person p
JOIN Movie m ON m.id = p.acted_in
----

[source,cypher,id=star_4a_expected]
----
MATCH (p:Person)-[acted_in:ACTED_IN]->(m:Movie)
RETURN elementId(p) AS `v$id`, p.born AS born, p.name AS name, m.title AS title
----

=== Returning nodes and relationships

A statement that projects a table alias such as

[source,sql,id=star_n,name=tableAlias]
----
SELECT m FROM Movie m
----

will result in a Cypher statement returning the matched node as node.

[source,cypher,id=star_n_expected]
----
MATCH (m:Movie)
RETURN m;
----

'''

A node can be aliased as well:

[source,sql,id=star_n2]
----
SELECT m AS node FROM Movie m
----

[source,cypher,id=star_n2_expected]
----
MATCH (m:Movie)
RETURN m AS node;
----

'''

Un-aliased tables can be used as well:

[source,sql,id=star_n3]
----
SELECT movie FROM Movie
----

[source,cypher,id=star_n3_expected]
----
MATCH (movie:Movie)
RETURN movie;
----

'''

Multiple entities are supported, too:

[source,sql,id=star_10]
----
SELECT p, r, m FROM Person p
JOIN ACTED_IN r ON r.person_id = p.id
JOIN Movie m ON m.id = r.movie_id
----

[source,cypher,id=star_10_expected]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie) RETURN p, r, m
----

== Comparing SQL with Cypher examples

The source of the following examples is: https://neo4j.com/developer/cypher/guide-sql-to-cypher/[Comparing SQL with Cypher].

=== Find all Products

=== Select and Return Records

Select everything from the `products` table.

[source,sql,id=t2_0,name=select_and_return_records,table_mappings=products:Product]
----
SELECT p.*
FROM products as p
----

Similarly, in Cypher, you just `MATCH` a simple pattern: all nodes with the *label* `Product` and `RETURN` them.

[source,cypher,id=t2_0_expected]
----
MATCH (p:Product)
RETURN p{.*} AS p
----

'''

The above query will project all properties of the matched node.
If you want to return the node itself, select it without using the asterisk:

[source,sql,id=t2_0a,name=select_and_return_records,table_mappings=products:Product]
----
SELECT p
FROM products as p
----

[source,cypher,id=t2_0a_expected]
----
MATCH (p:Product)
RETURN p
----

=== Field Access, Ordering and Paging

*It is more efficient to return only a subset of attributes*, like `ProductName` and `UnitPrice`.
And while we are at it, let's also order by price and only return the 10 most expensive items.
(Remember that labels, relationship-types and property-names are *case sensitive* in Neo4j.)

[source,sql,id=t2_1,name=field_acces_ordering_paging,table_mappings=products:Product]
----
SELECT p.`productName`, p.`unitPrice`
FROM products as p
ORDER BY p.`unitPrice` DESC
LIMIT 10
----

[source,cypher,id=t2_1_expected]
----
MATCH (p:Product)
RETURN p.productName, p.unitPrice ORDER BY p.unitPrice DESC LIMIT 10
----

'''

The default order direction will be translated as is:

[source,sql,id=t2_2,name=order_by_default]
----
SELECT * FROM Movies m ORDER BY m.title
----

[source,cypher,id=t2_2_expected,parseCypher=false]
----
MATCH (m:Movies)
RETURN * ORDER BY m.title
----

=== `DISTINCT` projections

The `DISTINCT` keyword for projections is handled:

[source,sql,id=t3_1,name=distinct]
----
SELECT DISTINCT m.released FROM Movies m
----

[source,cypher,id=t3_1_expected,parseCypher=false]
----
MATCH (m:Movies)
RETURN DISTINCT m.released
----

It works with `*` projections as well:

[source,sql,id=t3_2,name=distinct_star]
----
SELECT DISTINCT m.* FROM Movies m
----

[source,cypher,id=t3_2_expected,parseCypher=true]
----
MATCH (m:Movies)
RETURN DISTINCT m {.*} AS m
----

However, as the qualified asterisks will use metadata if available, the translation with a database connection is different:

[source,sql,id=t3_3,name=distinct_star_with_db,metaData=Movies:title|released]
----
SELECT DISTINCT m.* FROM Movies m
----

[source,cypher,id=t3_3_expected,parseCypher=false]
----
MATCH (m:Movies)
RETURN DISTINCT elementId(m) AS `v$id`, m.title AS title, m.released AS released
----

Note that each row includes the Neo4j element ID, making each row unique.
This being said, the `DISCTINCT` clause is of limited use with the asterisk.
