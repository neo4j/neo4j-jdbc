#
# Copyright (c) 2023-2025 "Neo4j,"
# Neo4j Sweden AB [https://neo4j.com]
#
# This file is part of Neo4j.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

getDatabaseProductName=CALL dbms.components() YIELD name, versions, edition \
WHERE name = 'Neo4j Kernel' \
UNWIND versions AS version \
RETURN name, edition, version

getDatabaseProductVersion=CALL dbms.components() YIELD name, versions \
WHERE name = 'Neo4j Kernel' \
UNWIND versions AS version \
RETURN version

getProcedures=SHOW PROCEDURES YIELD name AS PROCEDURE_NAME, description AS REMARKS \
ORDER BY PROCEDURE_NAME \
WHERE name = $name OR $name IS NULL \
RETURN \
$catalogAsParameterWorkaround AS PROCEDURE_CAT, \
'public' AS PROCEDURE_SCHEM, \
PROCEDURE_NAME, \
NULL AS reserved_1, \
NULL AS reserved_2, \
NULL AS reserved_3, \
REMARKS, \
$procedureType AS PROCEDURE_TYPE, \
PROCEDURE_NAME as SPECIFIC_NAME

getProcedureColumns=UNWIND $results AS result \
WITH result, range(0, size(result.returnDescriptions) - 1) AS ordinal_positions \
UNWIND ordinal_positions AS ORDINAL_POSITION \
WITH result, ORDINAL_POSITION \
WHERE result.returnDescriptions[ORDINAL_POSITION].name = $columnNamePattern OR $columnNamePattern IS NULL OR $columnNamePattern = '%' \
RETURN \
$catalogAsParameterWorkaround AS PROCEDURE_CAT, \
'public' AS PROCEDURE_SCHEM, \
result.name AS PROCEDURE_NAME, \
result.returnDescriptions[ORDINAL_POSITION].name AS COLUMN_NAME, \
$returnType AS COLUMN_TYPE, \
result.returnDescriptions[ORDINAL_POSITION].type AS DATA_TYPE, \
NULL AS TYPE_NAME, \
NULL AS PRECISION, \
NULL AS LENGTH, \
NULL AS SCALE, \
NULL AS RADIX, \
$nullable AS NULLABLE, \
result.returnDescriptions[ORDINAL_POSITION].description AS REMARKS, \
NULL AS COLUMN_DEF, \
NULL AS SQL_DATA_TYPE, \
NULL AS SQL_DATETIME_SUB, \
NULL AS CHAR_OCTET_LENGTH, \
ORDINAL_POSITION + 1 AS ORDINAL_POSITION, \
'' AS IS_NULLABLE, \
result.name AS SPECIFIC_NAME \
UNION ALL \
UNWIND $results AS result \
WITH result, range(0, size(result.argumentDescriptions) - 1) AS ordinal_positions \
UNWIND ordinal_positions AS ORDINAL_POSITION \
WITH result, ORDINAL_POSITION \
WHERE result.argumentDescriptions[ORDINAL_POSITION].name = $columnNamePattern OR $columnNamePattern IS NULL OR $columnNamePattern = '%' \
RETURN \
$catalogAsParameterWorkaround AS PROCEDURE_CAT, \
'public' AS PROCEDURE_SCHEM, \
result.name AS PROCEDURE_NAME, \
result.argumentDescriptions[ORDINAL_POSITION].name AS COLUMN_NAME, \
$columnType AS COLUMN_TYPE, \
result.argumentDescriptions[ORDINAL_POSITION].type AS DATA_TYPE, \
NULL AS TYPE_NAME, \
NULL AS PRECISION, \
NULL AS LENGTH, \
NULL AS SCALE, \
NULL AS RADIX, \
$nullable AS NULLABLE, \
result.argumentDescriptions[ORDINAL_POSITION].description AS REMARKS, \
NULL AS COLUMN_DEF, \
NULL AS SQL_DATA_TYPE, \
NULL AS SQL_DATETIME_SUB, \
NULL AS CHAR_OCTET_LENGTH, \
ORDINAL_POSITION + 1 AS ORDINAL_POSITION, \
'' AS IS_NULLABLE, \
result.name AS SPECIFIC_NAME

getTablesApoc=CALL db.info() yield name AS TABLE_CAT \
WITH TABLE_CAT \
CALL apoc.meta.schema({sample: $sampleSize}) YIELD value \
UNWIND keys(value) AS labelOrType \
WITH TABLE_CAT, labelOrType, value \
WHERE value[labelOrType].type = 'node' \
CALL { \
    WITH labelOrType, value \
    UNWIND keys(value[labelOrType].relationships) AS relationshipType \
    WITH labelOrType AS from, value, relationshipType \
    WHERE value[from].relationships[relationshipType].direction = 'out' \
    UNWIND value[from].relationships[relationshipType].labels AS to \
    WITH * \
    RETURN \
      from + '_'  + relationshipType + '_'  + to AS TABLE_NAME, \
      from + '\n' + relationshipType + '\n' + to AS REMARKS \
    UNION \
    RETURN NULL as TABLE_NAME, NULL AS REMARKS \
} \
WITH DISTINCT \
  TABLE_CAT, \
  COALESCE(TABLE_NAME, labelOrType) AS TABLE_NAME, \
  CASE \
    WHEN TABLE_NAME IS NULL THEN 'TABLE' \
    ELSE 'RELATIONSHIP' \
  END AS TABLE_TYPE, \
  REMARKS \
WHERE ($name IS NULL OR TABLE_NAME =~ $name) \
  AND ($types IS NULL OR ANY (type IN $types WHERE type = TABLE_TYPE)) \
RETURN \
  TABLE_CAT, \
  'public' AS TABLE_SCHEM, \
  TABLE_NAME, \
  TABLE_TYPE, \
  REMARKS, \
  NULL AS TYPE_CAT, \
  NULL AS TYPE_SCHEM, \
  NULL AS TYPE_NAME, \
  NULL AS SELF_REFERENCES_COL_NAME, \
  NULL AS REF_GENERATION \
UNION \
CALL db.info() yield name AS TABLE_CAT \
WITH TABLE_CAT \
UNWIND $views AS TABLE_NAME \
WITH TABLE_CAT, TABLE_NAME, 'CBV' AS TABLE_TYPE \
WHERE ($name IS NULL OR TABLE_NAME =~ $name) \
  AND ($types IS NULL OR ANY (type IN $types WHERE type = TABLE_TYPE)) \
RETURN \
  TABLE_CAT, \
  'public' AS TABLE_SCHEM, \
  TABLE_NAME, \
  TABLE_TYPE, \
  NULL AS REMARKS, \
  NULL AS TYPE_CAT, \
  NULL AS TYPE_SCHEM, \
  NULL AS TYPE_NAME, \
  NULL AS SELF_REFERENCES_COL_NAME, \
  NULL AS REF_GENERATION \
ORDER BY TABLE_TYPE DESC, TABLE_NAME

getTablesFallback=CALL db.labels() YIELD label AS TABLE_NAME \
WHERE ($name IS NULL OR TABLE_NAME =~ $name) AND EXISTS {MATCH (n) WHERE TABLE_NAME IN labels(n)} \
  AND ($types IS NULL OR ANY (type IN $types WHERE type = 'TABLE')) \
WITH TABLE_NAME CALL db.info() yield name AS TABLE_CAT \
WITH TABLE_NAME, TABLE_CAT \
RETURN \
  TABLE_CAT, \
  'public' AS TABLE_SCHEM, \
  TABLE_NAME, \
  'TABLE' as TABLE_TYPE, \
  NULL as REMARKS, \
  NULL AS TYPE_CAT, \
  NULL AS TYPE_SCHEM, \
  NULL AS TYPE_NAME, \
  NULL AS SELF_REFERENCES_COL_NAME, \
  NULL AS REF_GENERATION \
UNION \
CALL db.relationshipTypes() YIELD relationshipType \
WHERE ($types IS NULL OR ANY (type IN $types WHERE type = 'RELATIONSHIP')) \
CALL { \
  WITH relationshipType \
  MATCH (n)-[r WHERE type(r) = relationshipType]->(m) \
  WITH n, relationshipType, m ORDER BY COUNT {(n)-[r WHERE type(r) = relationshipType]->(m)} DESC LIMIT CASE WHEN $sampleSize < 0 THEN 4294967295 ELSE $sampleSize END \
  UNWIND labels(n) AS from \
  UNWIND labels(m) AS to \
  WITH DISTINCT from, to, relationshipType \
  RETURN from + '_'  + relationshipType + '_'  + to AS TABLE_NAME, \
         from + '\n' + relationshipType + '\n' + to AS REMARKS \
} \
WITH * WHERE ($name IS NULL OR TABLE_NAME =~ $name) \
WITH * CALL db.info() yield name AS TABLE_CAT \
WITH * \
RETURN \
  TABLE_CAT, \
  'public' AS TABLE_SCHEM, \
  TABLE_NAME, \
  'RELATIONSHIP' as TABLE_TYPE, \
  REMARKS, \
  NULL AS TYPE_CAT, \
  NULL AS TYPE_SCHEM, \
  NULL AS TYPE_NAME, \
  NULL AS SELF_REFERENCES_COL_NAME, \
  NULL AS REF_GENERATION \
UNION \
CALL db.info() yield name AS TABLE_CAT \
WITH TABLE_CAT \
UNWIND $views AS TABLE_NAME \
WITH TABLE_CAT, TABLE_NAME, 'CBV' AS TABLE_TYPE \
WHERE ($name IS NULL OR TABLE_NAME =~ $name) \
  AND ($types IS NULL OR ANY (type IN $types WHERE type = TABLE_TYPE)) \
RETURN \
  TABLE_CAT, \
  'public' AS TABLE_SCHEM, \
  TABLE_NAME, \
  TABLE_TYPE, \
  NULL AS REMARKS, \
  NULL AS TYPE_CAT, \
  NULL AS TYPE_SCHEM, \
  NULL AS TYPE_NAME, \
  NULL AS SELF_REFERENCES_COL_NAME, \
  NULL AS REF_GENERATION \
ORDER BY TABLE_TYPE DESC, TABLE_NAME

getColumns=// First part gets simple node properties \
\n CALL db.schema.nodeTypeProperties() YIELD nodeLabels, propertyName, propertyTypes WITH * \
\n WHERE ($name IS NULL OR $name = '%' OR ANY (label IN nodeLabels WHERE label =~ $name)) \
\n   AND propertyName IS NOT NULL \
\n   AND ($column_name IS NULL OR propertyName =~ $column_name) \
\n RETURN [x IN nodeLabels WHERE $name IS NULL OR $name = '%' OR x = $name] AS tables, \
\n        propertyName, propertyTypes, "TABLE" AS TABLE_TYPE, null AS relationshipType, \
\n        null AS SCOPE_TABLE \
\n UNION \
\n // Unionised with all relationship properties \
\n CALL db.schema.relTypeProperties() YIELD relType, propertyName, propertyTypes \
\n WITH substring(relType, 2, size(relType)-3) AS relationshipType, propertyName, propertyTypes \
\n WHERE ($column_name IS NULL OR propertyName =~ $column_name) \
\n CALL { \
\n   // Determining start and end of all relationships in question, to compute the virtual table name (and filter on that) \
\n   WITH relationshipType \
\n   MATCH (n)-[r WHERE type(r) = relationshipType]->(m) \
\n   WITH n, relationshipType, m ORDER BY COUNT {(n)-[r WHERE type(r) = relationshipType]->(m)} DESC LIMIT $sampleSize \
\n   UNWIND labels(n) AS from \
\n   UNWIND labels(m) AS to \
\n   RETURN DISTINCT from, to \
\n } \
\n WITH from, to, relationshipType, \
\n      propertyName AS relPropertyName, propertyTypes AS relPropertyTypes, \
\n      from + '_' + relationshipType + '_' + to AS tableName \
\n WHERE ($name IS NULL OR $name = '%' OR tableName =~ $name) \
\n CALL { \
\n   // Subquery to unionise the relationship properties with the properties of start and end node \
\n   WITH from, to, relPropertyName, relPropertyTypes \
\n   CALL db.schema.nodeTypeProperties() YIELD nodeLabels, propertyName, propertyTypes \
\n   WITH *, ANY (label IN nodeLabels WHERE label = from) AS is_start, ANY (label IN nodeLabels WHERE label = to) AS is_end  \
\n   WHERE (is_start OR is_end) \
\n     AND ($column_name IS NULL OR propertyName =~ $column_name) \
\n   RETURN propertyName, propertyTypes, \
\n          CASE WHEN is_start THEN from \
\n               WHEN is_end   THEN to   END AS SCOPE_TABLE \
\n   UNION \
\n   WITH relPropertyName, relPropertyTypes \
\n   RETURN relPropertyName AS propertyName, relPropertyTypes AS propertyTypes, null AS SCOPE_TABLE \
\n } \
\n RETURN [tableName] AS tables, propertyName, propertyTypes, "RELATIONSHIP" AS TABLE_TYPE, relationshipType, SCOPE_TABLE \
\n UNION \
\n UNWIND $viewColumns AS viewColumn \
\n WITH viewColumn \
\n WHERE ($name IS NULL OR $name = '%' OR viewColumn.viewName =~ $name) \
\n   AND ($column_name IS NULL OR viewColumn.propertyName  =~ $column_name) \
\n RETURN [viewColumn.viewName] AS tables, \
\n        viewColumn.propertyName AS propertyName, \
\n        [viewColumn.propertyType] AS propertyTypes, \
\n        "CBV" AS TABLE_TYPE, \
\n        null AS relationshipType, \
\n        null AS SCOPE_TABLE

getColumns.nullability=SHOW CONSTRAINTS YIELD * \
WHERE type IN ['NODE_KEY', 'NODE_PROPERTY_EXISTENCE', 'RELATIONSHIP_KEY', 'RELATIONSHIP_PROPERTY_EXISTENCE'] \
AND $propertyName IN properties \
AND ANY (x IN $nodeLabels WHERE x IN labelsOrTypes) \
RETURN count(*) > 0

getIndexInfo=SHOW INDEXES YIELD name, type, labelsOrTypes, properties, owningConstraint, entityType \
ORDER BY name \
WHERE ($name IS NULL OR $name = '%' OR $name IN labelsOrTypes) \
AND size(labelsOrTypes) = 1 \
AND entityType = 'NODE' \
AND type <> 'LOOKUP' \
AND (NOT $unique OR owningConstraint IS NOT NULL)

getIndexInfo.flattening=CALL db.info() yield name AS TABLE_CAT WITH TABLE_CAT \
UNWIND $results AS result \
WITH TABLE_CAT, result, range(0, size(result.properties) - 1) AS ordinal_positions \
UNWIND ordinal_positions AS ORDINAL_POSITION \
WITH TABLE_CAT, result, ORDINAL_POSITION \
RETURN \
TABLE_CAT, \
'public' AS TABLE_SCHEM, \
result.tableName AS TABLE_NAME, \
result.owningConstraint IS NULL AS NON_UNIQUE, \
result.name AS INDEX_QUALIFIER, \
result.name AS INDEX_NAME, \
$type AS TYPE, \
CASE WHEN result.properties[ORDINAL_POSITION] IS NULL THEN NULL ELSE ORDINAL_POSITION + 1 END AS ORDINAL_POSITION, \
result.properties[ORDINAL_POSITION] AS COLUMN_NAME, \
'A' AS ASC_OR_DESC, \
NULL AS CARDINALITY, \
NULL AS PAGES, \
NULL AS FILTER_CONDITION

getFunctions=SHOW FUNCTIONS YIELD name AS FUNCTION_NAME, description AS REMARKS \
ORDER BY FUNCTION_NAME \
WHERE name = $name OR $name IS NULL \
RETURN $catalogAsParameterWorkaround AS FUNCTION_CAT, \
'public' AS FUNCTION_SCHEM, \
FUNCTION_NAME, \
REMARKS, \
$functionType AS FUNCTION_TYPE, \
FUNCTION_NAME AS SPECIFIC_NAME

getFunctionColumns=UNWIND $results AS result \
RETURN \
$catalogAsParameterWorkaround AS FUNCTION_CAT, \
'public' AS FUNCTION_SCHEM, \
result.name AS FUNCTION_NAME, \
NULL AS COLUMN_NAME, \
$returnType AS COLUMN_TYPE, \
result.returnDescriptions AS DATA_TYPE, \
NULL AS TYPE_NAME, \
NULL AS PRECISION, \
NULL AS LENGTH, \
NULL AS SCALE, \
NULL AS RADIX, \
$nullable AS NULLABLE, \
NULL AS REMARKS, \
NULL AS CHAR_OCTET_LENGTH, \
0 AS ORDINAL_POSITION, \
'' AS IS_NULLABLE, \
result.name AS SPECIFIC_NAME \
UNION ALL \
UNWIND $results AS result \
WITH result, range(0, size(result.argumentDescriptions) - 1) AS ordinal_positions \
UNWIND ordinal_positions AS ORDINAL_POSITION \
WITH result, ORDINAL_POSITION \
WHERE result.argumentDescriptions[ORDINAL_POSITION].name = $columnNamePattern OR $columnNamePattern IS NULL OR $columnNamePattern = '%' \
RETURN \
$catalogAsParameterWorkaround AS FUNCTION_CAT, \
'public' AS FUNCTION_SCHEM, \
result.name AS FUNCTION_NAME, \
result.argumentDescriptions[ORDINAL_POSITION].name AS COLUMN_NAME, \
$columnType AS COLUMN_TYPE, \
result.argumentDescriptions[ORDINAL_POSITION].type AS DATA_TYPE, \
NULL AS TYPE_NAME, \
NULL AS PRECISION, \
NULL AS LENGTH, \
NULL AS SCALE, \
NULL AS RADIX, \
$nullable AS NULLABLE, \
result.argumentDescriptions[ORDINAL_POSITION].description AS REMARKS, \
NULL AS CHAR_OCTET_LENGTH, \
ORDINAL_POSITION + 1 AS ORDINAL_POSITION, \
'' AS IS_NULLABLE, \
result.name AS SPECIFIC_NAME

getArgumentDescriptions=SHOW %s YIELD name, description, argumentDescription, returnDescription \
ORDER BY name \
WHERE (name = $name OR $name IS NULL OR $name = '%%')

allProceduresAreCallable=SHOW PROCEDURE EXECUTABLE YIELD name AS PROCEDURE_NAME

getUserName=SHOW CURRENT USER YIELD user

getMaxConnections=SHOW SETTINGS YIELD * \
WHERE name =~ 'server.bolt.thread_pool_max_size' \
RETURN toInteger(value)

getPrimaryKeys=SHOW CONSTRAINTS YIELD name, labelsOrTypes, properties \
  WHERE ANY (v IN labelsOrTypes WHERE v = $name) \
  RETURN *

getCatalogs=SHOW DATABASES YIELD name AS TABLE_CAT ORDER BY TABLE_CAT

isReadOnly=SHOW DATABASES yield name, access WHERE name = $name RETURN access = 'read-only'
